
#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	СтруктураПолей = Новый Структура("ОбъектИсточник, ОбъектПриемник", "Источник", "Приемник");
	РеквизитыВладельца = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Объект.Владелец, СтруктураПолей);
	ЗаполнитьЗначенияСвойств(ЭтотОбъект, РеквизитыВладельца);
	
КонецПроцедуры

&НаКлиенте
Процедура ПриОткрытии(Отказ)
	
	// Редактор кода
	#Если ВебКлиент Тогда
		
	#Иначе
		НачатьПолучениеКаталогаВременныхФайлов(Новый ОписаниеОповещения("ЗагрузитьРедакторКода", ЭтотОбъект));
	#КонецЕсли
	
	ОбновитьТекущийАктивныйОбработчик();
	ОбновитьНаличиеОбработчиковАлгоритмов();
	
КонецПроцедуры

&НаКлиенте
Процедура ПередЗаписью(Отказ, ПараметрыЗаписи)
	
	// Редактор кода
	Объект.АлгоритмПередОбработкойВыгрузки = Элементы.АлгоритмПередОбработкойВыгрузкиРедактор.Документ.defaultView.editor.getValue();
	Объект.АлгоритмПередВыгрузкойСвойства = Элементы.АлгоритмПередВыгрузкойСвойстваРедактор.Документ.defaultView.editor.getValue();
	Объект.АлгоритмПриВыгрузкеСвойства = Элементы.АлгоритмПриВыгрузкеСвойстваРедактор.Документ.defaultView.editor.getValue();
	Объект.АлгоритмПослеВыгрузкиСвойства = Элементы.АлгоритмПослеВыгрузкиСвойстваРедактор.Документ.defaultView.editor.getValue();
	Объект.АлгоритмПослеОбработкиВыгрузки = Элементы.АлгоритмПослеОбработкиВыгрузкиРедактор.Документ.defaultView.editor.getValue();
	
КонецПроцедуры

&НаКлиенте
Процедура ПослеЗаписи(ПараметрыЗаписи)
	Оповестить("ЗаписьПравилаКонвертации");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовФорм

&НаКлиенте
Процедура ПриемникНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	
	Если ЗначениеЗаполнено(ОбъектПриемник) Тогда
		Оповещение = Новый ОписаниеОповещения("ПриемникНачалоВыбораЗавершение", ЭтотОбъект);
		мПараметрыФормы = Новый Структура("ВидОбъектаОбмена, ИмяРодительскогоОбъекта",
		ПредопределенноеЗначение("Перечисление.СМП_ВидыОбъектовОбмена.ГруппаСвойствОбъекта"), ОбъектПриемник);
		ОткрытьФорму("ОбщаяФорма.СМП_ФормаВыбораПриемника", мПараметрыФормы, Элемент,,,, Оповещение);
	КонецЕсли;
	
КонецПроцедуры

// Обработчик оповещения.
//
&НаКлиенте
Процедура ПриемникНачалоВыбораЗавершение(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Если РезультатЗакрытия <> Неопределено Тогда
		Объект.Приемник = РезультатЗакрытия.Имя;
		Объект.ИмяПриемника = РезультатЗакрытия.Имя;
		Объект.ВидПриемника = ПолучитьВидПриемника(РезультатЗакрытия.Вид);
		Если НЕ Модифицированность Тогда
			Модифицированность = Истина;
		КонецЕсли;
	КонецЕсли;
	
КонецПроцедуры //ПриемникНачалоВыбораЗавершение

&НаКлиенте
Процедура КомментарийНачалоВыбора(Элемент, ДанныеВыбора, СтандартнаяОбработка)
	ОбщегоНазначенияКлиент.ПоказатьФормуРедактированияКомментария(Элемент.ТекстРедактирования, ЭтотОбъект, "Объект.Комментарий");
КонецПроцедуры

&НаКлиенте
Процедура СтраницыАлгоритмыПриСменеСтраницы(Элемент, ТекущаяСтраница)
	ОбновитьТекущийАктивныйОбработчик();
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ОткрытьРедакторКода(Команда)
	
	ТекстОбработчика = Элементы[ТекущийАктивныйОбработчик + "Редактор"].Документ.defaultView.editor.getValue();
	мПараметры = Новый Структура("ТекстОбработчика", ТекстОбработчика);
	Оповещение = Новый ОписаниеОповещения("ПослеЗакрытияРедактораКода", ЭтотОбъект);
	ОткрытьФорму("Обработка.СМП_РедакторКода.Форма", мПараметры, ЭтотОбъект,,,, Оповещение, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
	
КонецПроцедуры

// Обработчик оповещения.
//
&НаКлиенте
Процедура ПослеЗакрытияРедактораКода(РезультатЗакрытия, ДополнительныеПараметры) Экспорт
	
	Элементы[ТекущийАктивныйОбработчик + "Редактор"].Документ.defaultView.editor.setValue(РезультатЗакрытия);
	
	Если РезультатЗакрытия <> Объект[ТекущийАктивныйОбработчик] Тогда
		Объект[ТекущийАктивныйОбработчик] = РезультатЗакрытия;
		Модифицированность = Истина;
		ОбновитьНаличиеОбработчиковАлгоритмов();
	КонецЕсли;
	
КонецПроцедуры //ПослеЗакрытияРедактораКода

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Редактор кода
&НаКлиенте
Процедура ЗагрузитьРедакторКода(ИмяКаталогаВременныхФайлов, ДополнительныеПараметры) Экспорт
	
	ДанныеПолей = Новый Соответствие;
	ДанныеПолей.Вставить("АлгоритмПередОбработкойВыгрузкиРедактор", Объект.АлгоритмПередОбработкойВыгрузки);
	ДанныеПолей.Вставить("АлгоритмПередВыгрузкойСвойстваРедактор", Объект.АлгоритмПередВыгрузкойСвойства);
	ДанныеПолей.Вставить("АлгоритмПриВыгрузкеСвойстваРедактор", Объект.АлгоритмПриВыгрузкеСвойства);
	ДанныеПолей.Вставить("АлгоритмПослеВыгрузкиСвойстваРедактор", Объект.АлгоритмПослеВыгрузкиСвойства);
	ДанныеПолей.Вставить("АлгоритмПослеОбработкиВыгрузкиРедактор", Объект.АлгоритмПослеОбработкиВыгрузки);
	СМП_РедакторКодаКлиент.ЗагрузитьРедактор(ЭтотОбъект, ДанныеПолей, ИмяКаталогаВременныхФайлов);
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьНаличиеОбработчиковАлгоритмов()

	Картинка = БиблиотекаКартинок.СинтаксическийКонтроль;
	
	Если ЗначениеЗаполнено(Объект.АлгоритмПередОбработкойВыгрузки)
			ИЛИ ЗначениеЗаполнено(Объект.АлгоритмПередВыгрузкойСвойства)
			ИЛИ ЗначениеЗаполнено(Объект.АлгоритмПриВыгрузкеСвойства)
			ИЛИ ЗначениеЗаполнено(Объект.АлгоритмПослеВыгрузкиСвойства)
			ИЛИ ЗначениеЗаполнено(Объект.АлгоритмПослеОбработкиВыгрузки)
		Тогда
		Элементы.СтраницаОбработчикиСобытий.Картинка = Картинка;
	Иначе
		Элементы.СтраницаОбработчикиСобытий.Картинка = Новый Картинка;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.АлгоритмПередОбработкойВыгрузки) Тогда
		Элементы.СтраницаАлгоритмПередОбработкойВыгрузки.Картинка = Картинка;
	Иначе
		Элементы.СтраницаАлгоритмПередОбработкойВыгрузки.Картинка = Новый Картинка;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.АлгоритмПередВыгрузкойСвойства) Тогда
		Элементы.СтраницаАлгоритмПередВыгрузкойСвойства.Картинка = Картинка;
	Иначе
		Элементы.СтраницаАлгоритмПередВыгрузкойСвойства.Картинка = Новый Картинка;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.АлгоритмПриВыгрузкеСвойства) Тогда
		Элементы.СтраницаАлгоритмПриВыгрузкеСвойства.Картинка = Картинка;
	Иначе
		Элементы.СтраницаАлгоритмПриВыгрузкеСвойства.Картинка = Новый Картинка;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.АлгоритмПослеВыгрузкиСвойства) Тогда
		Элементы.СтраницаАлгоритмПослеВыгрузкиСвойства.Картинка = Картинка;
	Иначе
		Элементы.СтраницаАлгоритмПослеВыгрузкиСвойства.Картинка = Новый Картинка;
	КонецЕсли;
	
	Если ЗначениеЗаполнено(Объект.АлгоритмПослеОбработкиВыгрузки) Тогда
		Элементы.СтраницаАлгоритмПослеОбработкиВыгрузки.Картинка = Картинка;
	Иначе
		Элементы.СтраницаАлгоритмПослеОбработкиВыгрузки.Картинка = Новый Картинка;
	КонецЕсли;
	
КонецПроцедуры

&НаКлиенте
Процедура ОбновитьТекущийАктивныйОбработчик() 
	
	ИмяТекВкладки = Элементы.СтраницыАлгоритмы.ТекущаяСтраница.Имя;
	ТекущийАктивныйОбработчик = СтрЗаменить(ИмяТекВкладки, "Страница", "");
	
КонецПроцедуры //ОбновитьТекущийАктивныйОбработчик

// Возвращает строковое представление для ВидПриемника.
//
// Параметры:
//   ВидПриемника - ПеречислениеСсылка.ВидыСвойств
// Возвращаемое значение:
//   Строка
//
&НаСервереБезКонтекста
Функция ПолучитьВидПриемника(ВидПриемника) 
	
	Возврат XMLСтрока(ВидПриемника);
	
КонецФункции // ПолучитьВидПриемника

#КонецОбласти
