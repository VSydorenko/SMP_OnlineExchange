
#Область ОписаниеПеременных

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	
	УстановитьВидимость();
	ЗаполнитьОбъектыКВыгрузкеНаФорме();
	
КонецПроцедуры

&НаСервере
Процедура ПередЗаписьюНаСервере(Отказ, ТекущийОбъект, ПараметрыЗаписи)
	
	ОбновитьОбъектыКВыгрузке(ТекущийОбъект);	
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура СпособПодключенияПриИзменении(Элемент)
	
    УстановитьВидимость();
	
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы_ОбъектыКВыгрузкеНаФорме

&НаКлиенте
Процедура ОбъектыКВыгрузкеНаФормеВыбор(Элемент, ВыбраннаяСтрока, Поле, СтандартнаяОбработка)
	
    СтандартнаяОбработка = Ложь;
	ТекДанные = Элементы.ОбъектыКВыгрузкеНаФорме.ТекущиеДанные;
	Если ТекДанные <> Неопределено И Поле.Имя = "ОбъектыКВыгрузкеНаФормеСиноним" Тогда
		ОткрытьФорму(ТекДанные.ИмяОбъекта + ".ФормаСписка");	
	КонецЕсли;	
	
КонецПроцедуры


#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ПроверитьПодключение(Команда)
	// Вставить содержимое обработчика.
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

// Выполняет установку видимости элементов формы.
//
&НаСервере
Процедура УстановитьВидимость() 
	
	ЭтоФайл 	  = (Объект.СпособПодключения = ПредопределенноеЗначение("Перечисление.СМП_ВидыТранспортовОбмена.FILE"));
	ЭтоХттпСервис = (Объект.СпособПодключения = ПредопределенноеЗначение("Перечисление.СМП_ВидыТранспортовОбмена.HS"));
	ЭтоКом 		  = (Объект.СпособПодключения = ПредопределенноеЗначение("Перечисление.СМП_ВидыТранспортовОбмена.COM"));
	
	Элементы.ГруппаFILE.Видимость = ЭтоФайл;
	Элементы.ГруппаHS.Видимость   = ЭтоХттпСервис;
	Элементы.ГруппаCOM.Видимость  = ЭтоКом;
	
КонецПроцедуры //УстановитьВидимость

// Заполняет таблицу объектов к выгрузке на форме.
//
&НаСервере
Процедура ЗаполнитьОбъектыКВыгрузкеНаФорме() 
	
	Картинка = БиблиотекаКартинок.ДокументОбъект;
	тзДок = ОбъектыКВыгрузкеНаФорме.Выгрузить();
	тзДок.Очистить();
	мдДокументы = Метаданные.Документы;
	
	Для Каждого мдДок Из мдДокументы Цикл
		НоваяСтрока = тзДок.Добавить();
		НоваяСтрока.Картинка = Картинка;
		НоваяСтрока.ИмяОбъекта = мдДок.ПолноеИмя();
		НоваяСтрока.Синоним = мдДок.Представление();
	КонецЦикла;
	
	Если Объект.ОбъектыКВыгрузке.Количество() > 0 Тогда
		
		Для Каждого СтрокаТЧ Из Объект.ОбъектыКВыгрузке Цикл
			ИскомаяСтр = тзДок.Найти(СтрокаТЧ.ИмяОбъекта, "ИмяОбъекта");
			Если ИскомаяСтр <> Неопределено Тогда
				ИскомаяСтр.Пометка = Истина;				
			КонецЕсли;
		КонецЦикла;	
	КонецЕсли;
	
	тзДок.Сортировать("Синоним");
	ОбъектыКВыгрузкеНаФорме.Загрузить(тзДок);
	
КонецПроцедуры //ЗаполнитьОбъектыКВыгрузкеНаФорме

// Заполняет таблицу объектов к выгрузке перед записью элемента спр..
//
// Параметры:
//   ТекущийОбъект - СправочникОбъект.СМП_УчетныеЗаписиОбменов - элемент в котором обновляется список объектов к выгрузке.
//
&НаСервере
Процедура ОбновитьОбъектыКВыгрузке(ТекущийОбъект) 
	
	тзОбъекты = ОбъектыКВыгрузкеНаФорме.Выгрузить(Новый Структура("Пометка", Истина));
	Если тзОбъекты.Количество() > 0 Тогда
		ТекущийОбъект.ОбъектыКВыгрузке.Загрузить(тзОбъекты);		
	КонецЕсли;	
	
КонецПроцедуры //ОбновитьОбъектыКВыгрузке


#КонецОбласти