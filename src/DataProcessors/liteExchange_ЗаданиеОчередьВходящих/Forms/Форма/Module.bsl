
////////////////////// СОХРАНЕНИЕ НАСТРОЕК РЕГЛ. ЗАДАНИЯ //////////////////////////

&НаКлиенте
Процедура СохранитьНастройкиРегламентногоЗадания(Команда)
	
	ОписаниеОшибки = СохранитьНастройкиРегламентногоЗаданияНаСервере();
	
	Если ПустаяСтрока(ОписаниеОшибки) Тогда
		ПоказатьПредупреждение(,"Настройки выполнения регл. задания успешно сохранены.", 20);
	Иначе
		ПоказатьПредупреждение(,ОписаниеОшибки, 20);
	КонецЕсли;
	
КонецПроцедуры

&НаСервере
Функция СохранитьНастройкиРегламентногоЗаданияНаСервере()
	
	ОписаниеОшибки = "";
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");
		
	ОбработкаРеглЗадания = ОбработкаОбъект.ПолучитьОбработкуРегламентногоЗадания(Истина, ОписаниеОшибки);
	
	Если ОбработкаРеглЗадания.Пустая() Тогда		
		ОписаниеОшибки = ОписаниеОшибки + "
		|Настройки выполнения регл. задания не сохранены!";
		Возврат ОписаниеОшибки;
	КонецЕсли;
		
	НастройкиРеглЗадания = Новый Структура;	
	Для каждого РеквизитОбработки Из ОбработкаОбъект.Метаданные().Реквизиты Цикл
		НастройкиРеглЗадания.Вставить(РеквизитОбработки.Имя, Объект[РеквизитОбработки.Имя]);
	КонецЦикла;
	
	ОбъектОбработкиРеглЗадания = ОбработкаРеглЗадания.ПолучитьОбъект();
	ОбъектОбработкиРеглЗадания.ХранилищеНастроек = Новый ХранилищеЗначения(НастройкиРеглЗадания);
	
	Попытка
		ОбъектОбработкиРеглЗадания.Записать();
	Исключение
		ОписаниеОшибки = "Не получилось записать элемент справочника ""Дополнительные отчеты и обработки"": " + ОбъектОбработкиРеглЗадания + "
		|Причина: " + ОписаниеОшибки();
	КонецПопытки;
	
	Возврат ОписаниеОшибки;
	
КонецФункции

////////////////////// ВЫПОЛНЕНИЕ РЕГЛ. ЗАДАНИЯ /////////////////////////

&НаКлиенте
Процедура ВыполнитьРегламентноеЗадание(Команда)
	ВыполнитьРегламентноеЗаданиеНаКлиенте();		
КонецПроцедуры

&НаКлиенте
Процедура ВыполнитьРегламентноеЗаданиеНаКлиенте(ИдентификаторКоманды = "")
	
	ОписаниеОшибки = ВыполнитьРегламентноеЗаданиеНаСервере(ИдентификаторКоманды);
	
	Если ТипЗнч(ОписаниеОшибки) = Тип("СписокЗначений") Тогда
		ОписаниеОповещенияВыбораКомандыРеглЗадания = Новый ОписаниеОповещения("ВыборКомандыРеглЗаданияЗавершение", ЭтаФорма);
		ОписаниеОшибки.ПоказатьВыборЭлемента(ОписаниеОповещенияВыбораКомандыРеглЗадания, "Выберите необходимое регл. задание");
	ИначеЕсли ПустаяСтрока(ОписаниеОшибки) Тогда	
		ПоказатьПредупреждение(,"Процедура регл. задания успешно выполнена.", 20);
	Иначе
		ПоказатьПредупреждение(,ОписаниеОшибки, 20);
 	КонецЕсли;	

КонецПроцедуры

&НаСервере
Функция ВыполнитьРегламентноеЗаданиеНаСервере(ИдентификаторКоманды = "")
	
	ОписаниеОшибки = "";
	
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");	
			
	Попытка
		ОбработкаОбъект.ВыполнитьКоманду(ИдентификаторКоманды);
	Исключение
		ОписаниеОшибки = "Не получилось выполнить процедуру регл. задания!
		|Причина: " + ОписаниеОшибки();
	КонецПопытки;
	
	Возврат ОписаниеОшибки;
	
КонецФункции

&НаКлиенте
Процедура ВыборКомандыРеглЗаданияЗавершение(ВыбраннаяКоманда, ДополнительныеПараметры) Экспорт
	
	Если ВыбраннаяКоманда = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ВыполнитьРегламентноеЗаданиеНаКлиенте(ВыбраннаяКоманда.Значение);

КонецПроцедуры

////////////////////// СОБЫТИЯ ФОРМЫ /////////////////////////

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
		
	ОбработкаОбъект = РеквизитФормыВЗначение("Объект");	
	Заголовок = "Параметры выполнения регл. задания """ + ОбработкаОбъект.Метаданные().Синоним + """";
	
	ОписаниеОшибки = ОбработкаОбъект.ПолучитьПараметрыВыполненияРегламентногоЗадания(Истина);
	
	Если ПустаяСтрока(ОписаниеОшибки) Тогда
		ЗначениеВРеквизитФормы(ОбработкаОбъект, "Объект");
	Иначе
		Сообщить(ОписаниеОшибки + "
		|Настройки выполнения регл. задания не восстановлены!");
	КонецЕсли;
		
КонецПроцедуры

&НаСервере
Процедура ОчиститьОчередьНаСервере()
	НаборЗаписей = РегистрыСведений.леОчередьВходящихСообщений.СоздатьНаборЗаписей();
	НаборЗаписей.Записать();
КонецПроцедуры

&НаКлиенте
Процедура ОчиститьОчередь(Команда)
	ОчиститьОчередьНаСервере();
КонецПроцедуры


&НаКлиенте
Процедура СписокПриАктивизацииСтроки(Элемент)
	ТекДанные = Элементы.Список.ТекущиеДанные;
	Если Не ТекДанные = Неопределено Тогда
		Объект.Идентификатор = ТекДанные.УникальныйИдентификатор;
	КонецЕсли;
КонецПроцедуры

